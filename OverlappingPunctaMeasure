run("Close All");
// Initialization and Splitting Channels
open();
filedir = getDirectory("image");
filename = getTitle();
selectImage(filename);
filein = filedir + filename; 
run("Set Scale...", "distance=0 known=0 pixel=1 unit=pixel");

fileNum = substring(filename, 0, 3);

// Manually open the zip files
waitForUser("Open CaMKII puncta ROI and the GluN2B puncta ROI Zip files in that order. Press OK.");

rename("Image");
run("Split Channels");
selectImage("C1-Image");
run("Duplicate...", "title=Measure duplicate");
selectImage("C2-Image");
run("Duplicate...", "title=Measure2 duplicate");
selectImage("C1-Image");
run("Stack to Images");
selectImage("C2-Image");
run("Stack to Images");

// Start creating the overlapping masks
for (i = 1; i <= 12; i++) {
    indexString = IJ.pad(i, 4);
    cImage = "C1-Image-" + indexString;
    gImage = "C2-Image-" + indexString;
    maskC = "MaskC" + IJ.pad(i, 2);
    maskG = "MaskG" + IJ.pad(i, 2);

    selectImage(cImage);
    roiManager("Select", i - 1);
    run("Create Mask");
    rename(maskC);

    selectImage(gImage);
    roiManager("Select", i + 11); // Adjust index for GluN2B ROIs
    run("Create Mask");
    rename(maskG);
}

// Calculate the overlapping ROIs for CaMKII channel
for (i = 1; i <= 12; i++) {
    maskC = "MaskC" + IJ.pad(i, 2);
    maskG = "MaskG" + IJ.pad(i, 2);
    resultName = "Result of " + maskC;
    punctaName = "CandGPuncta" + IJ.pad(i, 2);

    imageCalculator("AND create", maskC, maskG);
    selectImage(resultName);
    run("Create Selection");

    if (selectionType() >= 0) {
        roiManager("Add");
        roiManager("Select", roiManager("count") - 1);
        roiManager("Rename", punctaName);
    } else {
        print("No valid selection for " + maskC + " and " + maskG + ", adding dummy ROI.");
        makeRectangle(0, 0, 1, 1);
        roiManager("Add");
        roiManager("Select", roiManager("count") - 1);
        roiManager("Rename", "Dummy" + punctaName);
    }
}

// Remove old ROIs
for (i = 23; i >= 0; i--) {
    roiManager("Select", i);
    roiManager("Delete");
}

roiManager("Deselect");
roiManager("Save", filedir + fileNum + "CaMKIIandGluN2BPunctaOverlap_ROIset.zip");

// Loop through the ROIs and measure CaMKII channel at corresponding timepoints
selectImage("Measure");
for (i = 0; i < roiManager("count"); i++) {
    roiManager("select", i);
    timepoint = i + 1; // Assuming ROI index corresponds to timepoint
    setSlice(timepoint); // Navigate to the correct timepoint
    run("Measure");
}
saveAs("Results", filedir + fileNum + "CaMKIIoverlappingpuncta_results.csv");
selectWindow("Results"); run("Close");

// Loop through the ROIs and measure GluN2B channel at corresponding timepoints
selectImage("Measure2");
for (i = 0; i < roiManager("count"); i++) {
    roiManager("select", i);
    timepoint = i + 1; // Assuming ROI index corresponds to timepoint
    setSlice(timepoint); // Navigate to the correct timepoint
    run("Measure");
}
saveAs("Results", filedir + fileNum + "GluN2Boverlappingpuncta_results.csv");
selectWindow("Results"); run("Close"); roiManager("Reset");
run("Close All");
